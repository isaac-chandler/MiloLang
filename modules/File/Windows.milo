#import "Windows"
#import "String"

// @Incomplete add multiple return values 
read_entire_file :: (file: string) -> (#must result: []u8, success := true) {
	result: []u8
	
	filename := to_windows_string(file)
	
	handle := CreateFileW(filename, GENERIC_READ, FILE_SHARE_READ, null, OPEN_EXISTING, FILE_ATTRIBUTE_NORMAL | FILE_FLAG_SEQUENTIAL_SCAN)
	
	deallocate(filename)
	
	if handle == INVALID_HANDLE_VALUE {
		return result, false
	} else {
	
		if GetFileSizeEx(handle, cast() *result.count) {
			result.data = allocate(result.count)
			remaining := result
			
			
			while remaining {
				bytes_read: u32
				
				bytes_to_read := remaining.count
				
				if (bytes_to_read > 0xFFFF_FFFF) {
					bytes_to_read = 0xFFFF_FFFF
				}
				
				if !ReadFile(handle, remaining.data, cast() bytes_to_read, *bytes_read) || bytes_read != bytes_to_read {
					result.count = 0
					deallocate(result.data)
					result.data = null
					
					CloseHandle(handle)
					
					return result, false
				}
				
				remaining.count -= bytes_to_read
				remaining.data += bytes_to_read
			}
		} else {
			CloseHandle(handle)
		
			return result, false
		}
		
		CloseHandle(handle)
	}
	
	return result
}

write_entire_file :: (file: string, data: []u8) -> bool {
	write := data
	
	filename := to_windows_string(file)
	
	handle := CreateFileW(filename, GENERIC_WRITE, 0, null, CREATE_ALWAYS, FILE_ATTRIBUTE_NORMAL | FILE_FLAG_SEQUENTIAL_SCAN)
	
	if handle == INVALID_HANDLE_VALUE {
		return false
	} else {
		while write {
			bytes_written: u32
			
			bytes_to_write := write.count
			
			if (bytes_to_write > 0xFFFF_FFFF) {
				bytes_to_write = 0xFFFF_FFFF
			}
			
			if !WriteFile(handle, write.data, cast() bytes_to_write, *bytes_written) || bytes_written != bytes_to_write {
				CloseHandle(handle)
				
				return false
			}
			
			write.count -= bytes_to_write
			write.data += bytes_to_write
		}
		
		CloseHandle(handle)
	}
	
	return true
}

get_files_in_directory :: (file: string, include_folders := false) -> []string {
	filter :: (using find_data: *WIN32_FIND_DATAW, include_folders: bool) -> bool {
		if include_folders {
			if dwFileAttributes & FILE_ATTRIBUTE_DIRECTORY {
				if cFileName[0] == '.' && cFileName[1] == 0                         return false  // Ignore the .  directory listed by FindFirstFile
				if cFileName[0] == '.' && cFileName[1] == '.' && cFileName[2] == 0  return false  // Ignore the .. directory listed by FindFirstFile
			}
		
			return true
		} else {
			return !(dwFileAttributes & FILE_ATTRIBUTE_DIRECTORY)
		}
	}

	pattern := concatenate(file, "*", delimiter = "\\")
	defer deallocate(pattern.data)

	files: [..]string
	
	find_data: WIN32_FIND_DATAW
	
	windows_string := to_windows_string(pattern)
	defer deallocate(windows_string)
	
	find_handle := FindFirstFileW(windows_string, *find_data)
	
	if find_handle != INVALID_HANDLE_VALUE {
		defer FindClose(find_handle)
		
		if filter(*find_data, include_folders)  array_add(*files, from_windows_string(find_data.cFileName.data))
		
		while FindNextFileW(find_handle, *find_data) {
			if filter(*find_data, include_folders)  array_add(*files, from_windows_string(find_data.cFileName.data))
		}
	}
	
	return files
}